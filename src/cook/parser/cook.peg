// This file contains the PEG grammar definition that
// Mouse uses to generate our parser.

Config = Spacing Command* EOT ;
Command = ( BuildRule ) Spacing ;

BuildRule = RuleName LPAR ParamList RPAR ;

RuleName = Identifier ;

ParamList = Param ( COMMA Param )* COMMA? Spacing ;
Param = Key EQU Value ;

Key = Identifier ;
Value = ( StringValue / ListValue ) ;
StringValue = StringLiteral Spacing ;
ListValue = LBRK StringValue ( COMMA StringValue )* COMMA? RBRK Spacing ;

StringLiteral = "L"? (["] StringChar* ["] Spacing)+ ;
StringChar = Escape / ![\"\n\\] _ ;
Escape = SimpleEscape ;
SimpleEscape = "\\" ['\"?\\abfnrtv] ;

Identifier = IdNondigit IdChar* Spacing ;
IdNondigit = [a-z] / [A-Z] / [_] ;
IdChar = [a-z] / [A-Z] / [0-9] / [_] ;

//-------------------------------------------------------------------------
//  A.1.1  Lexical elements
//  Tokens are: Keyword, Identifier, Constant, StringLiteral, Punctuator.
//  Tokens are separated by Spacing.
//-------------------------------------------------------------------------

Spacing = ( WhiteSpace / LineComment )* ;

WhiteSpace  = [ \n\r\t\u000B\u000C] ;

LineComment = "#" (!"\n" _)* ;

//-------------------------------------------------------------------------
LBRK       =  "["         Spacing ;
RBRK       =  "]"         Spacing ;
LPAR       =  "("         Spacing ;
RPAR       =  ")"         Spacing ;

COLON      =  ":"  ![>]   Spacing ;
EQU        =  "="  !"="   Spacing ;
COMMA      =  ","         Spacing ;



//-------------------------------------------------------------------------
EOT        =  !_    ;
