// This file contains the PEG grammar definition that Mouse uses to generate our parser.
// http://en.wikipedia.org/wiki/Parsing_expression_grammar
//
// BuildConfig grammar

BuildConfig = Spacing Command* EOT {buildConfig} ;
Command = ( BuildRule ) Spacing {command} ;

BuildRule = RuleName LPAR ParamList RPAR {buildRule} ;

RuleName = Identifier {ruleName} ;

ParamList = Param ( COMMA Param )* COMMA? Spacing {&paramList} ;
Param = Key EQU Value {param} ;

Key = Identifier {key} ;
Value =
    (
    StringValue
    / ListStringValue
    / NumberValue
    / ListNumberValue
    ) {value} ;

StringValue = StringLiteral Spacing {stringValue} ;
NumberValue = IntegerConstant Spacing {numberValue} ;
ListStringValue = LBRK StringValue ( COMMA StringValue )* COMMA? RBRK {listStringValue} ;
ListNumberValue = LBRK NumberValue ( COMMA NumberValue )* COMMA? RBRK {listNumberValue} ;



//-----------------------------------------------------------------------//
//  Lexical elements                                                     //
//-----------------------------------------------------------------------//

//-------------------------------------------------------------------------
//  Keywords
//-------------------------------------------------------------------------

INCLUDE      = "include"       !IdChar Spacing ;
SUBINCLUDE   = "subclude"      !IdChar Spacing ;


//-------------------------------------------------------------------------
//  Constant
//-------------------------------------------------------------------------

IntegerConstant
    = ( DecimalConstant
      / HexConstant
      / OctalConstant
      )
    IntegerSuffix? Spacing {integerConstant}
    ;

DecimalConstant = [1-9][0-9]* {decimalConstant} ;

OctalConstant   = "0"[0-7]* {octalConstant} ;

HexConstant     = HexPrefix HexDigit+ {hexConstant} ;

HexPrefix       = "0x" / "0X" ;

HexDigit        = [a-f] / [A-F] / [0-9] ;

IntegerSuffix
    = [uU] Lsuffix?
    / Lsuffix [uU]?
    ;

Lsuffix
    = "ll"
    / "LL"
    / [lL]
    ;


//-------------------------------------------------------------------------
//  Identifier
//-------------------------------------------------------------------------

Identifier = IdNondigit IdChar* Spacing {identifier} ;
IdNondigit = [a-z] / [A-Z] ;
IdChar = [a-z] / [A-Z] / [0-9] / [_] ;


//-------------------------------------------------------------------------
//  Spacing
//-------------------------------------------------------------------------

Spacing = ( WhiteSpace / LineComment )* ;

WhiteSpace  = [ \n\r\t\u000B\u000C] ;

LineComment = "#" (!"\n" _)* EOL ;


//-------------------------------------------------------------------------
//  String Literals
//-------------------------------------------------------------------------

StringLiteral = ["] StringChar* ["] Spacing {stringLiteral} ;
StringChar = Escape / ![\"\n\\] _ ;

Escape = SimpleEscape ;
SimpleEscape = "\\" ['\"?\\abfnrtv] ;


//-------------------------------------------------------------------------
//  Punctuators
//-------------------------------------------------------------------------
LBRK       =  "["         Spacing ;
RBRK       =  "]"         Spacing ;
LPAR       =  "("         Spacing ;
RPAR       =  ")"         Spacing ;

EQU        =  "="  !"="   Spacing ;
COMMA      =  ","         Spacing ;

EOL        =  [\r\n] / !_ ;
EOT        =  !_    ;
